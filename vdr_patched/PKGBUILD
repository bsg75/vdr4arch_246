# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Manuel Reimer <manuel.reimer@gmx.de>
# Contriubtions: Christian Schuld <christian@schuld.org>
pkgbase=vdr
pkgname=(vdr vdr-examples)
pkgver=2.4.6
_vdrapi=2.4.6
pkgrel=2
url="http://tvdr.de/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('fontconfig' 'libcap' 'libjpeg-turbo' 'libsystemd' 'perl' 'ttf-font' 'systemd' 'ncurses')
source=("http://www.tvdr.de/ftp/${pkgbase}-${pkgver}.tar.bz2"
        "$pkgbase-$pkgver-clre-crash-fix.patch"
        'vdr-MainMenuHooks.patch'
        'http://schuld.org/vdr-2.4.0-source_dependent_new_channel_insertion.diff'
        'http://schuld.org/vdr-2.4.0_attric_usb_fakeCount.diff'
	'http://www.udo-richter.de/vdr/files/vdr-2.1.5-naludump-0.1.diff'
	'vdr-2.4.5-dynamite.patch::https://www.vdr-portal.de/index.php?attachment/44643'
	'http://schuld.org/vdr-2.4.6-freesat.diff'
        '00-vdr.conf' '50-hello.conf' '50-pictures.conf'
        '60-create-dvb-device-units.rules'
        'gen-sddropin'
        'shutdown.sh'
        'shutdown-wrapper.c'
        'vdr.service'
        'vdr.sysuser')
sha256sums=('c8993babf2a878a0fba84558de1e35f042c3c66f7c1ec569eea00a3af1014e4b'
            'd95115c3fb89d17be9367f882cf7581cb0cea3401fc6dcc5a0b25714ca43dac6'
            '4c553065d24ee4dc001c06ff588494db44982b7debe9a1e6cd1a8903beb7c87b'
            'b9525426fdc3a96a4002649071da9e3e28a4c1dd0e4e3260d9626d9bca2ac3f4'
            '4ddecc1e8b9dfa8e13a557b902131d899b530a1f97c1a67dd041fe437d00966e'
            '4606e6d78f4315a1e2f196740fb6f797b1e6dd201d3cd8cc44571730aacdc61f'
            '71c76dc3d890e63d98ec051441db131eb3a38369ee2a44b0343d85b3f1e2b8ad'
            'ea974dde52d6149222c52059124cfdef54e0279765b08870a1eee44446278dbf'
            '86f2469f459e2aabfc0ab703fc8435e458e89c4879376e900160d083924097b3'
            '423656cb6ba39af52d379dee697c52e6f435c098daa8c2ba429c1247b757af50'
            '39f4c2886328dc947dbef70dbc37d42504dc5c9d6a2ad81bf0c41d26fa3a5d6a'
            '1d914d6308a2b79ede34b7670788e5dfd3a8fab24156353555a66c9ef0bb41d7'
            '54a901d735d200ba3e2df4d9f45e1e537832a868c6ae5ae42ed3ff1a4c2c3f05'
            '7313a8db29693fa84d19be7b715b3ab04fd77eddcb52682588cc0a094764b6ba'
            '17b82fd995e9a39ad8d73d46b586d6c1934a6747c2859bd6d6df22ed254b5c4b'
            'f33f42a77bd93f00e7aa18f22667f0097468de76a0269c6e35fdc0fd2e30f6e6'
            'c6a6ed882190d487b1df3023d6b950685714ac75503ba40b4f6567ac1a983bad')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  echo 'CFLAGS      += -O3' > Make.config
  echo 'CXXFLAGS    += -O3' >> Make.config
  echo 'PREFIX       = /usr' >> Make.config
  echo 'LIBDIR       = /usr/lib/vdr/plugins' >> Make.config
  echo 'VDR_USER     = vdr' >> Make.config
  echo 'SDNOTIFY     = 1' >> Make.config
  echo 'LIRC_DEVICE  = /run/lirc/lircd' >> Make.config

  # save modified channels.conf every 60 seconds
  sed -i 's/CHANNELSAVEDELTA     600/CHANNELSAVEDELTA      60/g' vdr.c

  # Custom extensions
  patch -p1 -i "$srcdir/vdr-MainMenuHooks.patch"

  # Insert new channels below a source specific label
  patch -p1 -i "$srcdir/vdr-2.4.0-source_dependent_new_channel_insertion.diff"

  # Better user experience to repeated key strokes with the attric usb lirc device
  patch -p1 -i "$srcdir/vdr-2.4.0_attric_usb_fakeCount.diff"

  # naludump
  patch -p1 -i "$srcdir/vdr-2.1.5-naludump-0.1.diff"

  # dynamite 
  patch -p1 -i "$srcdir/vdr-2.4.5-dynamite.patch"

  # Freesat Patch
  patch -p1 -i "$srcdir/vdr-2.4.6-freesat.diff"

  # Upstream fixes
  patch -i "$srcdir/$pkgbase-$pkgver-clre-crash-fix.patch"

  # Don't install plugins with VDR
  sed -i '/^install: /s/install-plugins //' Makefile
}

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make
}

package_vdr() {
  pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
  depends=('fontconfig' 'libcap' 'libjpeg-turbo' 'libsystemd' 'perl' 'ttf-font')
  optdepends=('lirc-utils: remote control support'
              'vdr-xorg: To start X11')
  replaces=('runvdr-extreme')
  conflicts=('runvdr-extreme')
  provides=("vdr-api=$_vdrapi")
  install='vdr.install'
  options=(emptydirs)
  backup=('etc/vdr/conf.d/00-vdr.conf'
          'var/lib/vdr/camresponses.conf'
          'var/lib/vdr/channels.conf'
          'var/lib/vdr/diseqc.conf'
          'var/lib/vdr/keymacros.conf'
          'var/lib/vdr/scr.conf'
          'var/lib/vdr/sources.conf'
          'var/lib/vdr/svdrphosts.conf')

  # Ship this one empty so it's there even without plugin packages installed
  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  # This one has to be shipped empty or some plugins (streamdev) fail to build
  mkdir -p "$pkgdir/usr/lib/vdr/plugins"

  install -Dm644 00-vdr.conf "$pkgdir/etc/vdr/conf.d/00-vdr.conf"
  install -Dm644 60-create-dvb-device-units.rules "$pkgdir/usr/lib/udev/rules.d/60-create-dvb-device-units.rules"
  install -Dm644 vdr.service "$pkgdir/usr/lib/systemd/system/vdr.service"

  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  chgrp 666 "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  chmod u+s "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"

  install -Dm755 gen-sddropin "$pkgdir/usr/bin/vdr-gensddropin"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  #Install sysuser config
  install -Dm644 ${srcdir}/$pkgbase.sysuser "$pkgdir/usr/lib/sysusers.d/$pkgbase.conf"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  mkdir -p "$pkgdir/var/lib/vdr"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/usr/share/vdr/shutdown-hooks"

  chown -R 666:666 "$pkgdir/srv/vdr"
  chown -R 666:666 "$pkgdir/var/cache/vdr"
  chown -R 666:666 "$pkgdir/var/lib/vdr"
}

package_vdr-examples() {
  pkgdesc="Plugins for vdr to show some possible features"
  depends=("vdr-api=${_vdrapi}" "gcc-libs" 'ncurses')
  backup=("etc/vdr/conf.avail/"50-{epgtableid0,hello,osddemo,pictures,skincurses,status,svdrpdemo}.conf
          "etc/vdr/conf.avail/"50-svc{cli,svr}.conf)

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install-plugins

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  for i in hello pictures; do
    install -Dm644 "${srcdir}/50-$i.conf" "$pkgdir/etc/vdr/conf.avail/50-$i.conf"
  done
  for i in epgtableid0 osddemo skincurses status svc{cli,svr} svdrpdemo; do
    echo "[$i]" > "$pkgdir/etc/vdr/conf.avail/50-$i.conf"
  done
}
